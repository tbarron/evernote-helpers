#!/usr/bin/osascript
-- ======================================================================
-- Assign a (set of) tags to a (set of) notes
--
-- Usage: add-tags [-m|--multiple] "QUERY" tag1 tag2 ...
--
-- If QUERY matches more than one note and -m/--multiple is not present,
-- the script fails with the message "too many notes matched, --multiple
-- required".
-- ======================================================================
on run argv
    set multiple to false
    set clean_args to {}
    -- clean the args, removing '-m/--multiple' if present
    repeat with arg in argv
        if (arg as string) is "-m" or (arg as string) is "--multiple" then
            set multiple to true
        else
            set end of clean_args to arg
        end if
    end repeat

    set query_string to item 1 of clean_args
    set tag_text_list to item 2 thru (count of clean_args) of clean_args
    tell application "Evernote"
        set note_list to (find notes query_string)
        if count of note_list is 0 then
            do shell script "echo no notes matched"
        else if count of note_list is greater than 1 and not multiple then
            do shell script "echo too many notes matched, --multiple required"
        else
            set tag_list to {}
            repeat with tag_text in tag_text_list
                if (not (tag named tag_text exists)) then
                    set end of tag_list to (make new tag Â¬
                        with properties {name: tag_text})
                else
                    set end of tag_list to (tag named tag_text)
                end if
            end repeat
            assign tag_list to note_list
        end if
    end tell
end run
