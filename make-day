#!/usr/bin/osascript
-- ============================================================================
-- (Re-)create the diary note for a specified day
--
-- Usage: make-day yyyy.mmdd
--
-- The date is required. A new note will be generated with the template
-- contents appropriate for the given day.
-- ============================================================================
on run argv
    set parpath to POSIX path of ((path to me as text) & "::")
    set fpath to parpath & "/library.scpt"
    set lib to load script POSIX file fpath

    tell application "Evernote"
        set the_date to lib's ymd_parse(item 1 of argv)
        set day_title to lib's diary_title(the_date)
        set wkd to (weekday of the_date)

        if the_date is greater than (current date) then
            set tag_list to { "future", "diary" }
        else
            set tag_list to { "past", "diary" }
        end if

        set tlist to (find notes "tag:diary tag:template intitle:" & wkd)
        set template to (item 1 of tlist)

        set final to lib's duplicate_note(template, ¬
                                          day_title, ¬
                                          tag_list, ¬
                                          "Journal")
        open note window with final
        do shell script "echo " & (note link of final)
    end tell
end run
