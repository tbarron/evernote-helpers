#!/usr/bin/osascript
-- ============================================================================
-- Shift tags on notes for today and yesterday
--
-- Usage: daily-twiddle
--
-- It is safe to run this script multiple times in the same day.
-- ============================================================================
on run argv
    set cwd to (do shell script "pwd")
    set fpath to cwd & "/library.scpt"
    set lib to load script POSIX file fpath

    set today_title to lib's diary_title(current date)
    set yesterdate to (current date) - 24*3600
    set yester_title to lib's diary_title(yesterdate)

    activate application "Evernote"
    tell application "Evernote"
        set query to "intitle:" & yester_title
        set ylist to (find notes query)
        if (count of ylist) is greater than 1 then
            display dialog "'" & yester_title & "' matches multiple notes"
        end if

        set query to "intitle:" & today_title
        set tlist to (find notes query)
        if (count of tlist) is greater than 1 then
            display dialog "'" & today_title & "' matches multiple notes"
        end if

        set yester_note to item 1 of ylist
        lib's safe_rmtag(yester_note, tag named "today")
        lib's safe_addtag(yester_note, tag named "past")

        set today_note to item 1 of tlist
        lib's safe_rmtag(today_note, tag named "future")
        lib's safe_addtag(today_note, tag named "today")
        lib's safe_addtag(today_note, tag named "current")

        set yester_window to open note window with yester_note
        close yester_window
        open note window with today_note
    end tell
end run
